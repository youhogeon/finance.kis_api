plugins {
    id 'java-library'
    id 'signing'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

dependencies {
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.18.3'
    implementation group: 'jakarta.websocket', name: 'jakarta.websocket-client-api', version: '2.2.0'
    implementation group: 'org.glassfish.tyrus.bundles', name: 'tyrus-standalone-client', version: '2.2.0'
    implementation 'org.slf4j:slf4j-api:2.0.17'

    compileOnly 'org.projectlombok:lombok:1.18.36'
    annotationProcessor 'org.projectlombok:lombok:1.18.36'

    testImplementation libs.junit.jupiter
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    testImplementation 'ch.qos.logback:logback-classic:1.5.17'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.register('sourcesJar', Jar) {
    from sourceSets.main.allSource
    archiveClassifier.set('sources')
}

tasks.register('javadocJar', Jar) {
    from javadoc
    archiveClassifier.set('javadoc')
}

publishing {
    publications {
        KISClient(MavenPublication) {
            from components.java

            artifact tasks.sourcesJar
            artifact tasks.javadocJar

            artifactId = "${project.projectDir.name}"

            pom {
                name = 'KIS API Java Warpper'
                description = 'KIS(한국투자증권) API 호출 위한 Java Library. API Definition을 직접 정의하여 호출하거나, kis_api artifact를 통해 사전 정의된 API를 호출 가능.'
                url = 'https://github.com/youhogeon/finance.kis_api'
                licenses {
                    license {
                        name = 'WTFPL'
                        url = 'http://www.wtfpl.net/'
                    }
                }
                developers {
                    developer {
                        id = 'youhogeon'
                        name = 'YouHoGeon'
                        email = 'me@youhogeon.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/youhogeon/finance.kis_api.git'
                    developerConnection = 'scm:git:ssh://github.com/youhogeon/finance.kis_api.git'
                    url = 'https://github.com/youhogeon/finance.kis_api'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.KISClient
}